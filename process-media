#!/usr/bin/perl

use strict;
use warnings;

require 5.010;

use Data::Dumper;
use Getopt::Long qw/GetOptions/;
use Sys::CPU qw/cpu_count/;
use Term::ReadKey qw/ReadKey ReadMode/;
use threads;
use threads::shared;
use Thread::Queue;
use Time::HiRes qw/sleep/;
use YAML::Tiny;

# Own modules
use lib './lib';
use Photo;
use Video;

$SIG{'INT'}  = \&sig_handler;
$SIG{'TERM'} = \&sig_handler;

## VARS

my $config_file = 'process-media.yaml';
my $path;
my @threads;
my @objects;
my $opts = {};
our %options;

## MAIN

my $conf = YAML::Tiny->read($config_file);

&check_conf();

GetOptions(
    $opts,
    'type=s',
    'format=s',
    'max_threads=i',
    'keep_name',
    'verbose',
    'overwrite',
    'help',
) or die("Error in command line arguments\n");;

&check_params();

&set_options();

&get_objects();

print Dumper(\@objects);exit;

if (scalar @objects == 0) {
    print "No files to process, Exiting...\n";
    exit 0;
} else {
    print "Found ".scalar @objects." files...\n";
    ReadMode 4;
    my $key = '';
    while($key !~ /^(?:y|n)$/) {
        1 while defined ReadKey -1; # discard any previous input
        print "Proceed ? (y/n): ";
        $key = ReadKey 0;
        print "$key\n";
    }
    ReadMode 0;

    exit 0 if $key ne 'y';
}

my $queue_todo = new Thread::Queue;
my $queue_done = new Thread::Queue;

my $run: shared = 1;
$queue_todo->enqueue($_) for @objects;

# start threads
while ($options{'max_threads'} >= 1) {
    push @threads, threads->new(\&process);
    $options{'max_threads'}--;
}

# wait first run to finish
sleep 0.05 while $queue_done->pending() < scalar @objects;

$queue_done->end;

# search duplicate name
'Photo'->search_duplicate(@objects);

$run = 2;
$queue_todo->enqueue($_) for @objects;
$queue_todo->end;

# waiting for threads to finish
$_->join for @threads;

exit 0;

## FUNCTIONS

sub process {

    $SIG{'INT'} = sub { threads->exit(); };

    while (my $obj = $queue_todo->dequeue) {
        if ($run == 1) {
            $obj->rename();
            $queue_done->enqueue($obj)
        }
        elsif ($run == 2) {
            $obj->exist();
            if (ref($obj) eq 'Photo') {
                $obj->create();
                $obj->rotate();
                $obj->resize();
                $obj->compress();
                $obj->strip();
            }
            elsif (ref($obj) eq 'Video') {
                $obj->encode();
                $obj->delete_tags();
                $obj->thumbnail();
            }
            $obj->integrity();
        }
    }

    threads->exit();
}

sub sig_handler {
    my $signame = shift;

    # send SIGINT to all running threads and detach it
    foreach my $thr (threads->list) {
        $thr->kill('INT')->detach();
    }

    die "Received a SIG$signame, Exiting...\n";
}

sub set_options {

    # max_threads
    if (defined $opts->{'max_threads'}) {
        $options{'max_threads'} = ($opts->{'max_threads'} == 0) ? Sys::CPU::cpu_count() : $opts->{'max_threads'};
    } else {
        $options{'max_threads'} = ($conf->[0]->{'max_threads'} == 0) ? Sys::CPU::cpu_count() : $conf->[0]->{'max_threads'};
    }

    # verbose
    $options{'verbose'} = $opts->{'verbose'} // $conf->[0]->{'verbose'};

    # overwrite
    $options{'overwrite'} = $opts->{'overwrite'} // $conf->[0]->{'overwrite'};

    # keep_name
    $options{'keep_name'} = $opts->{'keep_name'} // $conf->[0]->{'keep_name'};

    $options{'regex_photo'} = $conf->[0]->{'regex_photo'};
    $options{'regex_video'} = $conf->[0]->{'regex_video'};

    # format
    if ($opts->{'format'}) {
        $options{'format'} = $opts->{'format'};
    } else {
        $options{'format'} = join(',', keys %{ $conf->[1] });
    }

    # type
    $options{'type'} = $opts->{'type'} // 'photo,video';

    # format
    $options{'allformat'} = $conf->[1];

    $path = $ARGV[0] // './';

    print Dumper(\%options);

    return 1;
}

sub check_conf {

    # options
    foreach (keys %{ $conf->[0] }) {
        die "Unknown options '$_' in config file\n"
            if $_ !~ /^(overwrite|regex_photo|regex_video|max_threads|keep_name|verbose)$/;

        # max_threads
        die "Miss/Bad value for '$_' in config file\n"
            if $_ eq 'max_threads' and $conf->[0]->{$_} !~ /^\d+$/;

        # keep_name
        die "Miss/Bad value for '$_' in config file\n"
            if $_ eq 'keep_name' and $conf->[0]->{$_} !~ /^(true|false)$/;

        # verbose
        die "Miss/Bad value for '$_' in config file\n"
            if $_ eq 'verbose' and $conf->[0]->{$_} !~ /^(true|false)$/;

        # overwrite
        die "Miss/Bad value for '$_' in config file\n"
            if $_ eq 'overwrite' and $conf->[0]->{$_} !~ /^(true|false)$/;
    }

    # format
    foreach my $fname (keys %{ $conf->[1] }) {
        die "Bad format name '$fname' in config file" if $fname !~ /^[\w-]+$/;

        foreach (keys %{ $conf->[1]->{$fname} }) {
            die "Unknown format options '$_' in config file\n"
                if $_ !~ /^(output_dir|type|rotate|compress|strip|resize|codec|reencode)$/;

            # type
            die "Bad format options '$_' in config file\n"
                if $_ eq 'type' and $conf->[1]->{$fname}->{$_} !~ /^(photo|video)$/;

            # output_dir
            die "Bad format options '$_' in config file\n"
                if $_ eq 'output_dir' and $conf->[1]->{$fname}->{$_} !~ /^[\w-]+$/;

            # rotate
            die "Bad format options '$_' in config file\n"
                if $_ eq 'rotate' and $conf->[1]->{$fname}->{$_} !~ /^(true|false)$/;

            # reencode
            die "Bad format options '$_' in config file\n"
                if $_ eq 'reencode' and $conf->[1]->{$fname}->{$_} !~ /^(true|false)$/;

            # resize
            die "Bad format options '$_' in config file\n"
                if $_ eq 'resize' and $conf->[1]->{$fname}->{$_} !~ /^[\dx><]+$/;

            # strip
            die "Bad format options '$_' in config file\n"
                if $_ eq 'strip' and $conf->[1]->{$fname}->{$_} !~ /^[\w-]+$/;

            # compress
            die "Bad format options '$_' in config file\n"
                if $_ eq 'compress' and $conf->[1]->{$fname}->{$_} !~ /^\d+$/ and $conf->[1]->{$fname}->{$_} > 100;
        }
    }

    return 1;
}

sub check_params {

    &usage() if $opts->{'help'};

    # type
    if (defined $opts->{'type'}) {
        foreach (split(',',$opts->{'type'})) {
            die "Bad type of files to process: $_, see help (-h) for supported type\n"
                if !/^(photo|video)$/;
        }
    }

    # format
    if (defined $opts->{'format'}) {
        foreach (split(',',$opts->{'format'})) {
            die "Unknown format '$_'\n" if not defined $conf->[1]->{$_};
        }
    }

    die "Incorrect parameters, see help (-h) for correct syntax\n" if @ARGV > 1;

    return 1;
}

sub get_files {

    my @files;

    if (-f $path) {
        push @files, $path;
    }
    elsif (-d $path) {
        $path .= '/' unless $path =~ /\/$/;
        opendir my $dh, $path or die "Failed to read directory \'$path\': $!\n";
        foreach (sort grep {!/^\./} readdir $dh) {
            push @files, $path.$_;
        }
        closedir $dh;
    }

    return @files;
}

sub get_objects {

    my @files = &get_files();

    foreach (@files) {
        my $obj;
        if (/$conf->[0]->{'options'}->{'regex_photo'}/i) {
            next if $options{'type'} !~ /photo/;
            $obj = new Photo;
        }
        elsif (/$conf->[0]->{'options'}->{'regex_video'}/i) {
            next if $options{'type'} !~ /video/;
            $obj = new Video;
        }
        if (defined $obj) {
            $obj->init($_, \%options);
            push @objects, $obj;
        }
    }

    return 1;
}

sub usage {
    print<<EOF;
Usage: $0 [options...] <path>
Options:
-t,--type\t\t{photos,videos}\tType of files to process (default: photos,videos)
-f,--format\t\t{archive,web}\tFormat of files to generate (default: archive,web)
-m,--max_threads\t<num_threads>\tMaximum allowed threads (default: number of cpu(s)/core(s))
-k,--keep_name\t\t\t\tDo not rename file, by default file are renamed like this: YYYYMMDD-HHMMSS
-v,--verbose\t\t\t\tVerbose output
-o,--overwrite\t\t\t\tOverwrite existing files
-h,--help\t\t\t\tThis help text
EOF
    exit 0;
}

## POD

=pod

=head1 NAME

process_media.pl

=head1 DESCRIPTION

This script will process (resize, compress...) photos and videos according to the specified options.

It depends on the following linux binary:

=over 4

=item *

ffmpeg

=item *

jpeginfo

=back

It needs the following ubuntu packages installed:

=over 4

=item *

ffmpeg

=item *

jpeginfo

=item *

libimage-exiftool-perl

=item *

libimage-magick-perl

=item *

libsys-cpu-perl

=item *

libterm-readkey-perl

=back

=head1 AUTHOR

Laurent Lavaud

=cut
